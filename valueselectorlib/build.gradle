apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
}

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = 'com.narvancomputer'
        pom.artifactId = 'ValueSelectorLib'
        pom.version = '1.0.0'
        // Add other pom properties here if you want (developer details / licenses)
        repository(url: "file:///c:/test/")
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'ValueSelectorLib'

    publishedGroupId = 'com.narvancomputer'
    libraryName = 'ValueSelectorLib'
    artifact = 'ValueSelectorLib'

    libraryDescription = 'Numeric TextView with add & minus btn'

    siteUrl = 'https://github.com/mehdi-salehi/ValueSelectorLib'
    gitUrl = 'https://github.com/mehdi-salehi/ValueSelectorLib.git'

    libraryVersion = '1.0.0'

    developerId = 'mehdi-salehi'
    developerName = 'mehdi salehi'
    developerEmail = 'meh2fari@gmail.com'

    licenseName = 'AGPL-V3'
    licenseUrl = 'http://www.opensource.org/licenses/agpl-v3.html'
    allLicenses = ["AGPL-V3"]
}
